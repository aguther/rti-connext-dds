apply from: "../prepareRtiHost.gradle"

// --------------------------------------------------------------------------------------------------------------------
def targetDirectory = "/opt/rti/connext-dds/recording-service"

ospackage {
    summary = "RTI Recording Service"
    description = ""

    packageGroup = "Applications/Communications"

    into "$targetDirectory"

    // runtime dependencies
    requires("libc.so.6()(64bit)")
    requires("libdl.so.2()(64bit)")
    requires("libm.so.6()(64bit)")
    requires("libnsl.so.1()(64bit)")
    requires("libpthread.so.0()(64bit)")
    requires("librt.so.1()(64bit)")

    // we provide the recording service
    provides("rti-connext-dds-recording-service", "${project.version}", EQUAL)

    // conflicts with SDK
    conflicts("rti-connext-dds-sdk-pro")

    // update old major versions
    obsoletes("rti-connext-dds-52-recording-service")

    // add recording service
    from("$extractionDirectoryResult") {
        include "bin/rtirecconv"
        include "bin/rtirecord"
        include "bin/rtirecsh"
        include "doc/manuals/recording_service/*"
        include "resource/app/bin/x64Linux2.6gcc4.1.1/rtirecconv"
        include "resource/app/bin/x64Linux2.6gcc4.1.1/rtirecord"
        include "resource/app/bin/x64Linux2.6gcc4.1.1/rtirecsh"
        include "resource/app/lib/*/libndds*.so"
        include "resource/app/lib/*/librtidlc.so"
        include "resource/schema/rti_record.*"
        include "resource/scripts/*"
        exclude "resource/scripts/rticommon_config.sh"
        include "resource/xml/RTI_RECORDING_SERVICE.xml"
        into ""
    }
    from("$extractionDirectoryResult") {
        include "resource/scripts/rticommon_config.sh"
        filter { String line ->
            line.replace("# copy_examples=false", "copy_examples=false\ncopy_workspace=false")
        }
        into ""
    }
    // add scripts
    from("src/main/resources/scripts") {
        into ""
    }
}

buildRpm {
    dependsOn prepareRtiHost

    // define local installation path for links
    def applicationLinkPath        = "/usr/bin"
    def applicationLauncherPath    = "$targetDirectory/launcher"

    // recording service
    link("$applicationLinkPath/rtirecord",  "$applicationLauncherPath")
    link("$applicationLinkPath/rtirecconv", "$applicationLauncherPath")
    link("$applicationLinkPath/rtirecsh",   "$applicationLauncherPath")
}

// --------------------------------------------------------------------------------------------------------------------
defaultTasks 'buildRpm'
