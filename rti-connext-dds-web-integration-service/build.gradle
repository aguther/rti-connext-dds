apply from: "../prepareRtiHost.gradle"

// --------------------------------------------------------------------------------------------------------------------
def targetDirectory = "/opt/rti/connext-dds/web-integration-service"

ospackage {
    summary = "RTI Web Integration Service"
    description = ""

    packageGroup = "Applications/Communications"

    into "$targetDirectory"

    // runtime dependencies
    requires("libc.so.6()(64bit)")
    requires("libdl.so.2()(64bit)")
    requires("libm.so.6()(64bit)")
    requires("libnsl.so.1()(64bit)")
    requires("libpthread.so.0()(64bit)")
    requires("librt.so.1()(64bit)")

    // we provide the web integration service
    provides("rti-connext-dds-web-integration-service", "${project.version}", EQUAL)

    // conflicts with SDK
    conflicts("rti-connext-dds-sdk-pro")

    // update old major versions
    obsoletes("rti-connext-dds-52-web-integration-service")
    obsoletes("rti-connext-dds-53-web-integration-service")

    // add web integration service
    from("$extractionDirectoryResult") {
        include "bin/rtiwebintegrationservice"
        include "doc/manuals/web_integration_service/*"
        include "resource/app/bin/x64Linux2.6gcc4.4.5/rtiwebintegrationservice"
        include "resource/app/lib/**"
        include "resource/schema/rti_web_integration_service.*"
        include "resource/schema/definitions/rti_dds_profiles_definitions.xsd"
        include "resource/scripts/*"
        exclude "resource/scripts/rticommon_config.sh"
        include "resource/xml/RTI_WEB_INTEGRATION_SERVICE.xml"
        into ""
    }
    from("$extractionDirectoryResult") {
        include "resource/scripts/rticommon_config.sh"
        filter { String line ->
            line.replace("# copy_examples=false", "copy_examples=false\ncopy_workspace=false")
        }
        into ""
    }
    // add scripts
    from("src/main/resources/scripts") {
        into ""
    }
}

buildRpm {
    dependsOn prepareRtiHost

    // define local installation path for links
    def applicationLinkPath        = "/usr/bin"
    def applicationLauncherPath    = "$targetDirectory/launcher"

    // web integration service
    link("$applicationLinkPath/rtiwebintegrationservice", "$applicationLauncherPath")
}

// --------------------------------------------------------------------------------------------------------------------
defaultTasks 'buildRpm'
