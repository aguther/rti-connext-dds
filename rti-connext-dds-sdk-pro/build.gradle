// --------------------------------------------------------------------------------------------------------------------
plugins {
    id 'nebula.ospackage' version '3.5.0'
    id 'net.researchgate.release' version '2.3.5'
}

// --------------------------------------------------------------------------------------------------------------------
def parsedVersion = VersionNumber.parse(project.version)
def extractionDirectory = "$buildDir/extracted"

task extractPackages {

    inputs.file "$projectDir/../packages/rti_connext_dds-${parsedVersion.major}.${parsedVersion.minor}.${parsedVersion.micro}-pro-host-x64Linux.run"
    outputs.dir extractionDirectory

    doLast {
        // variables
        def commandLine
        def processObject
 
        // install SDK into extraction directory (with prefix)
        commandLine = "$projectDir/../packages/rti_connext_dds-${parsedVersion.major}.${parsedVersion.minor}.${parsedVersion.micro}-pro-host-x64Linux.run --mode unattended --prefix $extractionDirectory"
        processObject = commandLine.execute()
        processObject.in.eachLine { line -> logger.info(line) }
        processObject.err.eachLine { line -> logger.error(line) }
        processObject.waitFor()
        assert !processObject.exitValue()

        // remove local launcher file
        delete file ("${System.properties['user.home']}/.local/share/applications/rtilauncher.desktop")
    }
}

// --------------------------------------------------------------------------------------------------------------------
def targetDirectory = "/opt/rti/connext-dds/sdk"

ospackage {
    vendor = "Real-Time Innovations (RTI)"
    url = "http://www.rti.com"
    license = "http://www.rti.com/downloads/license-agreement.html"

    summary = "RTI Connext® DDS Professional SDK"
    description = "RTI Connext® DDS is the first connectivity platform designed for the demanding requirements of the Industrial Internet of Things (IIoT). It delivers the low-latency and real-time QoS needed to monitor and control physical-world processes. It provides the non-stop availability and security essential for mission-critical systems. And it complies with the Data Distribution Service (DDS) standard to foster interoperability and an open architecture, slashing lifecycle costs."

    packageGroup = "Development/Tools"

    version = "${project.version}"
    release = "$rpmRelease"
    arch = X86_64
    os = LINUX

    user = "root"

    into "$targetDirectory"

    // we need a license to work
    requires("rti-connext-dds-license")
    // we need target libraries to be installed to work
    requires("rti-connext-dds-sdk-pro-target", "${parsedVersion.major}.${parsedVersion.minor}.${parsedVersion.micro}", EQUAL)

    // we provide the admin console
    provides("rti-connext-dds-admin-console", "${parsedVersion.major}.${parsedVersion.minor}.${parsedVersion.micro}", EQUAL)

    // ensure we only install one package of SDK
    provides("rti-connext-dds-sdk-pro", "${parsedVersion.major}.${parsedVersion.minor}.${parsedVersion.micro}", EQUAL)
    conflicts("rti-connext-dds-sdk-pro", "${parsedVersion.major}.${parsedVersion.minor}.${parsedVersion.micro}", GREATER | LESS)
    conflicts("rti-connext-dds-sdk-core")

    // add SDK content
    from("$extractionDirectory/rti_connext_dds-${parsedVersion.major}.${parsedVersion.minor}.${parsedVersion.micro}") {
        exclude "**/rtilauncher.desktop"
        exclude "**/uninstall"
        into ""
    }
    // add desktop file with correct paths
    from("$extractionDirectory/rti_connext_dds-${parsedVersion.major}.${parsedVersion.minor}.${parsedVersion.micro}") {
        include "**/rtilauncher.desktop"
        filter { String line ->
            line.replace("$extractionDirectory/rti_connext_dds-${parsedVersion.major}.${parsedVersion.minor}.${parsedVersion.micro}", "$targetDirectory")
        }
        filter { String line ->
            line.replace("$targetDirectory/bin/rtilauncher", "/usr/bin/rtilauncher")
        }
        into ""
    }
    // add desktop links
    from("src/main/resources/desktop-files") {
        into ""
    }
    // add scripts
    from("src/main/resources/scripts") {
        into ""
    }
}

buildRpm {
    dependsOn extractPackages

    // define local installation path for links
    def applicationLinkPath        = "/usr/bin"
    def applicationLauncherPath    = "$targetDirectory/launcher"
    def applicationDesktopLinkPath = "/usr/share/applications"
    def applicationIconLinkPath    = "/usr/share/icons/hicolor/96x96/apps"
    def javaLinkPath               = "/usr/lib/java"
    def javaInstallPath            = "$targetDirectory/lib/java"
    def alternativesPriority       = 1

    // create link to license file
    link("$targetDirectory/rti_license.dat", "/opt/rti/connext-dds/license/rti_license.dat")

    // create desktop link to RTI launcher
    link("$applicationDesktopLinkPath/rtilauncher.desktop", "$targetDirectory/rtilauncher.desktop")

    // links for applications without any alternative
    link("$applicationLinkPath/jms2dds",                "$applicationLauncherPath")
    link("$applicationLinkPath/rtiddsgen",              "$applicationLauncherPath")
    link("$applicationLinkPath/rtiddsgen_server",       "$applicationLauncherPath")
    link("$applicationLinkPath/rtiddsping",             "$applicationLauncherPath")
    link("$applicationLinkPath/rtiddsprototyper",       "$applicationLauncherPath")
    link("$applicationLinkPath/rtiddsspy",              "$applicationLauncherPath")
    link("$applicationLinkPath/rtilauncher",            "$applicationLauncherPath")
    link("$applicationLinkPath/rtimonitor",             "$applicationLauncherPath")
    link("$applicationLinkPath/rtipkginstall",          "$applicationLauncherPath")
    link("$applicationLinkPath/rtirecconv",             "$applicationLauncherPath")
    link("$applicationLinkPath/rtirecord",              "$applicationLauncherPath")
    link("$applicationLinkPath/rtirecordingconsole",    "$applicationLauncherPath")
    link("$applicationLinkPath/rtirecsh",               "$applicationLauncherPath")
    link("$applicationLinkPath/rtireplay",              "$applicationLauncherPath")
    link("$applicationLinkPath/rtireplaysh",            "$applicationLauncherPath")
    link("$applicationLinkPath/rtishapesdemo",          "$applicationLauncherPath")

    // this will be called after installation phase
    postInstall """
        # applications
        alternatives --install '$applicationLinkPath/rtiadminconsole'                'rtiadminconsole'         '$applicationLauncherPath'                                                      $alternativesPriority \\
                     --slave   '$applicationDesktopLinkPath/rtiadminconsole.desktop' 'rtiadminconsole.desktop' '$targetDirectory/rtiadminconsole.desktop'                                                            \\
                     --slave   '$applicationIconLinkPath/rtiadminconsole.xpm'        'rtiadminconsole.xpm'     '$targetDirectory/resource/app/app_support/admin_console/x64Linux/icon.xpm'

        # enable auto configuration
        alternatives --auto 'rtiadminconsole'
    """

    // this will be called after uninstallation phase
    postUninstall """
        # check if uninstall is due to an upgrade
        if [ '\$1' = '1' ]; then
            # we are in an upgrade process >> nothing to do
            exit 0
        fi

        # applications
        alternatives --remove 'rtiadminconsole' '$applicationLauncherPath'
    """
}

// --------------------------------------------------------------------------------------------------------------------
defaultTasks 'buildRpm'