apply from: "../prepareRtiHost.gradle"

// --------------------------------------------------------------------------------------------------------------------
def targetDirectory = "/opt/rti/connext-dds/database-integration-service"

ospackage {
    summary = "RTI Database Integration Service"
    description = ""

    packageGroup = "Applications/Communications"

    into "$targetDirectory"

    // runtime dependencies
    requires("libc.so.6()(64bit)")
    requires("libdl.so.2()(64bit)")
    requires("libm.so.6()(64bit)")
    requires("libnsl.so.1()(64bit)")
    requires("libpthread.so.0()(64bit)")
    requires("librt.so.1()(64bit)")
    //requires("libodbc.so.1()(64bit)")

    // we provide the database integration service
    provides("rti-connext-dds-database-integration-service", "${project.version}", EQUAL)

    // conflicts with SDK
    conflicts("rti-connext-dds-sdk-pro")

    // add database integration service
    from("$extractionDirectoryResult") {
        include "bin/rtirtc_mysql"
        include "bin/rtirtc_oracle"
        include "bin/rtirtc_postgresql"
        include "bin/rtirtc_timesten"
        include "doc/manuals/database_integration_service/*"
        include "resource/app/bin/x64Linux2.6gcc4.1.1/rtirtc_mysql"
        include "resource/app/bin/x64Linux2.6gcc4.1.1/rtirtc_oracle"
        include "resource/app/bin/x64Linux2.6gcc4.1.1/rtirtc_postgresql"
        include "resource/app/bin/x64Linux2.6gcc4.1.1/rtirtc_timesten"
        include "resource/app/lib/*/libndds*.so"
        include "resource/app/lib/*/librti*.so"
        include "resource/schema/rti_real_time_connect.*"
        include "resource/scripts/*"
        exclude "resource/scripts/rticommon_config.sh"
        include "resource/xml/RTI_REAL_TIME_CONNECT.xml"
        into ""
    }
    from("$extractionDirectoryResult") {
        include "resource/scripts/rticommon_config.sh"
        filter { String line ->
            line.replace("# copy_examples=false", "copy_examples=false\ncopy_workspace=false")
        }
        into ""
    }
    // add scripts
    from("src/main/resources/scripts") {
        into ""
    }
}

buildRpm {
    dependsOn prepareRtiHost

    // define local installation path for links
    def applicationLinkPath        = "/usr/bin"
    def applicationLauncherPath    = "$targetDirectory/launcher"

    // database integration service
    link("$applicationLinkPath/rtirtc_mysql",      "$applicationLauncherPath")
    link("$applicationLinkPath/rtirtc_oracle",     "$applicationLauncherPath")
    link("$applicationLinkPath/rtirtc_postgresql", "$applicationLauncherPath")
    link("$applicationLinkPath/rtirtc_timesten",   "$applicationLauncherPath")
}

// --------------------------------------------------------------------------------------------------------------------
defaultTasks 'buildRpm'
