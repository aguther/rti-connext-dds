apply from: "../prepareRtiHostWithTarget.gradle"

// --------------------------------------------------------------------------------------------------------------------
def targetDirectory = "/opt/rti/connext-dds/routing-service"

ospackage {
    summary = "RTI Routing Service"
    description = "Welcome to RTI Routing Service, an out- of-the-box solution for integrating disparate and geo-graphically dispersed systems. It scales RTI ConnextÂ® DDS applications across domains, LANs and WANs, including firewall and NAT traversal."

    packageGroup = "Applications/Communications"

    into "$targetDirectory"

    // runtime dependencies
    requires("java")
    requires("libc.so.6()(64bit)")
    requires("libdl.so.2()(64bit)")
    requires("libm.so.6()(64bit)")
    requires("libnsl.so.1()(64bit)")
    requires("libpthread.so.0()(64bit)")
    requires("librt.so.1()(64bit)")

    // requires to base packages
    requires("jpackage-utils")

    // requires runtime
    requires("rti-connext-dds(abi)", "${project.version}", EQUAL)

    // we provide the routing service
    provides("rti-connext-dds-routing-service", "${project.version}", EQUAL)

    // conflicts with SDK
    conflicts("rti-connext-dds-sdk-pro")

    // update old major versions
    obsoletes("rti-connext-dds-52-routing-service")
    obsoletes("rti-connext-dds-53-routing-service")

    // add routing service
    from("$extractionDirectoryResult") {
        include "bin/rtiroutingservice"
        include "bin/rtirssh"
        include "doc/manuals/routing_service/*"
        include "resource/app/bin/x64Linux2.6gcc4.4.5/rtiroutingservice"
        include "resource/app/bin/x64Linux2.6gcc4.4.5/rtirssh"
        include "resource/app/lib/**"
        include "resource/schema/rti_routing_service*"
        include "resource/schema/definitions/rti_service_common_definitions.xsd"
        include "resource/scripts/*"
        exclude "resource/scripts/rticommon_config.sh"
        include "resource/xml/RTI_ROUTING_SERVICE.xml"
        into ""
    }
    from("$extractionDirectoryResult") {
        include "resource/scripts/rticommon_config.sh"
        filter { String line ->
            line.replace("# copy_examples=false", "copy_examples=false\ncopy_workspace=false")
        }
        into ""
    }
    // add scripts
    from("src/main/resources/scripts") {
        into ""
    }
}

buildRpm {
    dependsOn prepareRtiHostWithTarget

    // define local installation path for links
    def applicationLinkPath        = "/usr/bin"
    def applicationLauncherPath    = "$targetDirectory/launcher"

    // routing service
    link("$applicationLinkPath/rtiroutingservice",      "$applicationLauncherPath")
    link("$applicationLinkPath/rtirssh",                "$applicationLauncherPath")
}

// --------------------------------------------------------------------------------------------------------------------
defaultTasks 'buildRpm'
