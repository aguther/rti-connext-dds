apply from: "../prepareRtiHost.gradle"

// --------------------------------------------------------------------------------------------------------------------
def targetDirectory = "/opt/rti/connext-dds/cloud-discovery-service"

ospackage {
    summary = "RTI Cloud Discovery Service"
    description = ""

    packageGroup = "Applications/Communications"

    into "$targetDirectory"

    // runtime dependencies
    requires("libc.so.6()(64bit)")
    requires("libdl.so.2()(64bit)")
    requires("libm.so.6()(64bit)")
    requires("libnsl.so.1()(64bit)")
    requires("libpthread.so.0()(64bit)")
    requires("librt.so.1()(64bit)")

    // requires to base packages
    requires("chkconfig")

    // we provide the cloud discovery service
    provides("rti-connext-dds-cloud-discovery-service", "${project.version}", EQUAL)

    // conflicts with SDK
    conflicts("rti-connext-dds-sdk-pro")

    // add cloud discovery service
    from("$extractionDirectoryResult") {
        include "bin/rticlouddiscoveryservice"
        include "doc/manuals/cloud_discovery_service/*"
        include "resource/app/bin/x64Linux2.6gcc4.1.1/rticlouddiscoveryservice"
        include "resource/app/lib/*/libndds*.so"
        include "resource/app/lib/*/librticonnextmsgc.so"
        include "resource/app/lib/*/librtidlc.so"
        include "resource/app/lib/*/librtirouting*.so"
        include "resource/app/lib/*/librtirs*.so"
        include "resource/app/lib/*/librtixml2.so"
        include "resource/schema/rti_cloud_discovery_service.*"
        include "resource/scripts/*"
        exclude "resource/scripts/rticommon_config.sh"
        include "resource/xml/RTI_CLOUD_DISCOVERY_SERVICE.xml"
        into ""
    }
    from("$extractionDirectoryResult") {
        include "resource/scripts/rticommon_config.sh"
        filter { String line ->
            line.replace("# copy_examples=false", "copy_examples=false\ncopy_workspace=false")
        }
        into ""
    }
    // add scripts
    from("src/main/resources/scripts") {
        into ""
    }
}

buildRpm {
    dependsOn prepareRtiHost

    // define local installation path for links
    def applicationLinkPath        = "/usr/bin"
    def applicationLauncherPath    = "$targetDirectory/launcher"

    // cloud discovery service
    link("$applicationLinkPath/rticlouddiscoveryservice", "$applicationLauncherPath")
}

// --------------------------------------------------------------------------------------------------------------------
defaultTasks 'buildRpm'
