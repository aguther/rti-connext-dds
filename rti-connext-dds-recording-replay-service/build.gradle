apply from: "../prepareRtiHost.gradle"

// --------------------------------------------------------------------------------------------------------------------
def targetDirectory = "/opt/rti/connext-dds/recording-replay-service"

ospackage {
    summary = "RTI Recording/Replay Service"
    description = ""

    packageGroup = "Applications/Communications"

    into "$targetDirectory"

    // runtime dependencies
    requires("libc.so.6()(64bit)")
    requires("libdl.so.2()(64bit)")
    requires("libm.so.6()(64bit)")
    requires("libnsl.so.1()(64bit)")
    requires("libpthread.so.0()(64bit)")
    requires("librt.so.1()(64bit)")

    // we provide the recording service
    provides("rti-connext-dds-recording-service", "${project.version}", EQUAL)
    provides("rti-connext-dds-replay-service", "${project.version}", EQUAL)

    // conflicts with SDK
    conflicts("rti-connext-dds-sdk-pro")

    // update old major versions
    obsoletes("rti-connext-dds-52-recording-service")
    obsoletes("rti-connext-dds-52-replay-service")
    obsoletes("rti-connext-dds-53-recording-service")
    obsoletes("rti-connext-dds-53-replay-service")

    // add recording service
    from("$extractionDirectoryResult") {
        include "bin/rticonverter"
        include "bin/rtirecordingservice_list_tags"
        include "bin/rtirecordingservice"
        include "bin/rtireplayservice"
        include "bin/rtixmlconverter"
        include "doc/manuals/recording_service/*"
        include "resource/app/bin/x64Linux2.6gcc4.4.5/rtirecordingservice"
        include "resource/app/bin/x64Linux2.6gcc4.4.5/rtireplayservice"
        include "resource/app/lib/**"
        include "resource/schema/rti_record.*"
        include "resource/schema/rti_replay.*"
        include "resource/schema/definitions/rti_recorder_common_definitions.xsd"
        include "resource/scripts/*"
        exclude "resource/scripts/rticommon_config.sh"
        include "resource/xml/RTI_RECORDING_SERVICE.xml"
        include "resource/xml/RTI_REPLAY_SERVICE.xml"
        into ""
    }
    from("$extractionDirectoryResult") {
        include "resource/scripts/rticommon_config.sh"
        filter { String line ->
            line.replace("# copy_examples=false", "copy_examples=false\ncopy_workspace=false")
        }
        into ""
    }
    // add scripts
    from("src/main/resources/scripts") {
        into ""
    }
}

buildRpm {
    dependsOn prepareRtiHost

    // define local installation path for links
    def applicationLinkPath        = "/usr/bin"
    def applicationLauncherPath    = "$targetDirectory/launcher"

    // recording service
    link("$applicationLinkPath/rticonverter",                   "$applicationLauncherPath")
    link("$applicationLinkPath/rtirecordingservice_list_tags",  "$applicationLauncherPath")
    link("$applicationLinkPath/rtirecordingservice",            "$applicationLauncherPath")
    link("$applicationLinkPath/rtireplayservice",               "$applicationLauncherPath")
    link("$applicationLinkPath/rtixmlconverter",                "$applicationLauncherPath")
}

// --------------------------------------------------------------------------------------------------------------------
defaultTasks 'buildRpm'
