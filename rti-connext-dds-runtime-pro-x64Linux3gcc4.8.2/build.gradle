// --------------------------------------------------------------------------------------------------------------------
plugins {
    id 'nebula.ospackage' version '3.5.0'
    id 'net.researchgate.release' version '2.3.5'
}

// --------------------------------------------------------------------------------------------------------------------
def extractionDirectory = "$buildDir/extracted"

task extractPackages {

    inputs.file "$projectDir/../packages/rti_connext_dds-5.2.0-pro-host-x64Linux.run"
    inputs.file "$projectDir/../packages/rti_connext_dds-5.2.0-pro-target-x64Linux3gcc4.8.2.rtipkg"
    outputs.dir extractionDirectory

    doLast {
        // variables
        def commandLine
        def processObject

        // install SDK into extraction directory (with prefix)
        commandLine = "$projectDir/../packages/rti_connext_dds-5.2.0-pro-host-x64Linux.run --mode unattended --prefix $extractionDirectory"
        processObject = commandLine.execute()
        processObject.in.eachLine { line -> logger.info(line) }
        processObject.err.eachLine { line -> logger.error(line) }
        processObject.waitFor()
        assert !processObject.exitValue()

        // install target libraries with RTI installer
        commandLine = "$extractionDirectory/rti_connext_dds-5.2.0/bin/rtipkginstall -u $projectDir/../packages/rti_connext_dds-5.2.0-pro-target-x64Linux3gcc4.8.2.rtipkg"
        processObject = commandLine.execute()
        processObject.in.eachLine { line -> logger.info(line) }
        processObject.err.eachLine { line -> logger.error(line) }
        processObject.waitFor()
        assert !processObject.exitValue()

        // remove local launcher file
        delete file ("${System.properties['user.home']}/.local/share/applications/rtilauncher.desktop")
    }
}

// --------------------------------------------------------------------------------------------------------------------
def parsedVersion = VersionNumber.parse(project.version)
def targetDirectory = "/opt/rti/connext-dds/runtime/x64Linux3gcc4.8.2"

ospackage {
    vendor = "Real-Time Innovations (RTI)"
    url = "http://www.rti.com"
    license = "http://www.rti.com/downloads/license-agreement.html"

    summary = "RTI Connext® DDS Professional - Runtime x64Linux3gcc4.8.2"
    description = "Runtime Libraries for x64Linux3gcc4.8.2 of RTI Connext® DDS Professional."

    packageGroup = "Development/Tools"

    version = "${project.version}"
    release = "$rpmRelease"
    arch = X86_64
    os = LINUX

    user = "root"

    into "$targetDirectory"

    // the following provides tell us what abi and languages we can serve
    provides("rti-connext-dds(abi)", "${parsedVersion.major}.${parsedVersion.minor}", EQUAL)
    provides("rti-connext-dds-runtime-c", "${parsedVersion.major}.${parsedVersion.minor}", EQUAL)
    provides("rti-connext-dds-runtime-cpp", "${parsedVersion.major}.${parsedVersion.minor}", EQUAL)
    provides("rti-connext-dds-runtime-java", "${parsedVersion.major}.${parsedVersion.minor}", EQUAL)

    // add java libraries
    from("$extractionDirectory/rti_connext_dds-5.2.0/lib/java") {
        include "**/nddsjava.jar"
        include "**/distlog*.jar"
        include "**/rticonnextmsg.jar"
        include "**/rtijms.jar"
        exclude "**/*d.*"
        into "java"
    }
    // add native runtime libraries
    from("$extractionDirectory/rti_connext_dds-5.2.0/lib/x64Linux3gcc4.8.2") {
        include "**/*.so"
        exclude "**/librtirs*.so"
        exclude "**/librtiroutingservice.so"
        exclude "**/*d.*"
        into "lib"
    }
}

buildRpm {
    dependsOn extractPackages

    // define local installation path for links
    def javaLinkPath         = "/usr/lib/java"
    def javaInstallPath      = "$targetDirectory/java"
    def libraryLinkPath      = "/usr/lib64"
    def libraryInstallPath   = "$targetDirectory/lib"
    def alternativesPriority = 10

    // this will be called after installation phase
    postInstall """
        # libraries
        alternatives --install '$libraryLinkPath/libnddscore.so'              'libnddscore.so'             '$libraryInstallPath/libnddscore.so'             $alternativesPriority \\
                     --slave   '$libraryLinkPath/libnddsc.so'                 'libnddsc.so'                '$libraryInstallPath/libnddsc.so'                                      \\
                     --slave   '$libraryLinkPath/libnddscpp.so'               'libnddscpp.so'              '$libraryInstallPath/libnddscpp.so'                                    \\
                     --slave   '$libraryLinkPath/libnddscpp2.so'              'libnddscpp2.so'             '$libraryInstallPath/libnddscpp2.so'                                   \\
                     --slave   '$libraryLinkPath/libnddsjava.so'              'libnddsjava.so'             '$libraryInstallPath/libnddsjava.so'                                   \\
                     --slave   '$libraryLinkPath/libnddstransporttcp.so'      'libnddstransporttcp.so'     '$libraryInstallPath/libnddstransporttcp.so'                           \\
                     --slave   '$libraryLinkPath/librticonnextmsgc.so'        'librticonnextmsgc.so'       '$libraryInstallPath/librticonnextmsgc.so'                             \\
                     --slave   '$libraryLinkPath/librticonnextmsgcpp.so'      'librticonnextmsgcpp.so'     '$libraryInstallPath/librticonnextmsgcpp.so'                           \\
                     --slave   '$libraryLinkPath/librtidlc.so'                'librtidlc.so'               '$libraryInstallPath/librtidlc.so'                                     \\
                     --slave   '$libraryLinkPath/librtidlcpp.so'              'librtidlcpp.so'             '$libraryInstallPath/librtidlcpp.so'                                   \\
                     --slave   '$libraryLinkPath/librtimonitoring.so'         'librtimonitoring.so'        '$libraryInstallPath/librtimonitoring.so'                              \\
                     --slave   '$javaLinkPath/distlog.jar'                    'distlog.jar'                '$javaInstallPath/distlog.jar'                                         \\
                     --slave   '$javaLinkPath/distlogdatamodel.jar'           'distlogdatamodel.jar'       '$javaInstallPath/distlogdatamodel.jar'                                \\
                     --slave   '$javaLinkPath/nddsjava.jar'                   'nddsjava.jar'               '$javaInstallPath/nddsjava.jar'                                        \\
                     --slave   '$javaLinkPath/rticonnextmsg.jar'              'rticonnextmsg.jar'          '$javaInstallPath/rticonnextmsg.jar'                                   \\
                     --slave   '$javaLinkPath/rtijms.jar'                     'rtijms.jar'                 '$javaInstallPath/rtijms.jar'                                          \\

        # update ld configuration
        ldconfig
    """

    // this will be called after uninstallation phase
    postUninstall """
        # check if uninstall is due to an upgrade
        if [ '\$1' = '1' ]; then
            # we are in an upgrade process >> nothing to do
            exit 0
        fi

        # libraries
        alternatives --remove 'libnddscore.so' '$libraryInstallPath/libnddscore.so'

        # update ld configuration
        ldconfig
    """
}

// --------------------------------------------------------------------------------------------------------------------
defaultTasks 'buildRpm'