// --------------------------------------------------------------------------------------------------------------------
plugins {
    id 'nebula.ospackage' version '3.4.0'
    id 'net.researchgate.release' version '2.3.5'
}

// --------------------------------------------------------------------------------------------------------------------
def extractionDirectory = "$buildDir/extracted"

task extractPackages {

    outputs.dir extractionDirectory

    doLast {
        copy {
            from tarTree(resources.gzip('../packages/RTI_Monitor-5.1.0-Linux.tar.gz'))
            into "$extractionDirectory"
        }
    }
}

// --------------------------------------------------------------------------------------------------------------------
def parsedVersion = VersionNumber.parse(project.version)
def targetDirectory = "/opt/rti/connext-dds/monitor"

ospackage {
    vendor = "Real-Time Innovations (RTI)"
    url = "http://www.rti.com"
    license = "http://www.rti.com/downloads/license-agreement.html"

    summary = "RTI Monitor"
    description = "RTI Monitor is a graphical tool that displays monitoring data from RTI ConnextÂ® (formerly RTI Data Distribution Service) applications."

    packageGroup = "Development/Tools"

    version = "${project.version}"
    release = "$rpmRelease"
    arch = X86_64
    os = LINUX

    user = "root"

    into "$targetDirectory"

    requires("rti-connext-dds-license")

    from("$extractionDirectory/RTI_Monitor_5.1.0") {
        into ""
    }
    from("src/main/resources/desktop-files") {
        into ""
    }
    from("src/main/resources/scripts") {
        into ""
    }
}

buildRpm {
    dependsOn extractPackages

    // create link to license file
    link("$targetDirectory/rti_license.dat", "/opt/rti/connext-dds/license/rti_license.dat")

    // create link for application
    link("/usr/bin/rtimonitor", "$targetDirectory/launcher")
    link("/usr/share/applications/rtimonitor.desktop", "$targetDirectory/rtimonitor.desktop")
}

// --------------------------------------------------------------------------------------------------------------------
defaultTasks 'clean', 'buildRpm'