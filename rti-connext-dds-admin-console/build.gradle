apply from: "../prepareRtiHost.gradle"

// --------------------------------------------------------------------------------------------------------------------
def targetDirectory = "/opt/rti/connext-dds/admin-console"

ospackage {
    vendor = "Real-Time Innovations (RTI)"
    url = "http://www.rti.com"
    license = "http://www.rti.com/downloads/license-agreement.html"

    summary = "RTI Administration Console"
    description = "RTI Administration Console is a centralized tool for monitoring and administering your distributed system. Admin Console collects various system health information and summarizes it in one easy-to-read table."

    packageGroup = "Development/Tools"

    into "$targetDirectory"

    // runtime dependencies
    requires("java")
    requires("libc.so.6()(64bit)")
    requires("libgcc_s.so.1()(64bit)")
    requires("libm.so.6()(64bit)")
    requires("libpthread.so.0()(64bit)")
    requires("librt.so.1()(64bit)")
    requires("libstdc++.so.6()(64bit)")

    // admin console requires a license to work
    requires("rti-connext-dds-license")

    // we provide the admin console
    provides("rti-connext-dds-admin-console", "${project.version}", EQUAL)

    // conflicts with SDK
    conflicts("rti-connext-dds-sdk-pro")

    // update old major versions
    obsoletes("rti-connext-dds-52-admin-console")
    obsoletes("rti-connext-dds-53-admin-console")

    // add admin console
    from("$extractionDirectoryResult") {
        include "bin/rtiadminconsole"
        include "doc/manuals/admin_console/*"
        include "resource/app/app_support/admin_console/**"
        include "resource/app/app_support/launcher/icons/admin_32x32.gif"
        include "resource/app/app_support/launcher/icons/Admin90.gif"
        include "resource/app/app_support/launcher/icons/AdminConsole2_32x32.png"
        include "resource/app/app_support/launcher/icons/AdminConsole2_90.png"
        include "resource/app/bin/x64Linux2.6gcc4.4.5/RTI_AdminConsole"
        include "resource/app/eclipse/**"
        include "resource/app/jre/**"
        include "resource/app/lib/**"
        include "resource/scripts/*"
        exclude "resource/scripts/rticommon_config.sh"
        include "resource/xml/admin_console_example_profiles.xml"
        into ""
    }
    from("$extractionDirectoryResult") {
        include "resource/scripts/rticommon_config.sh"
        filter { String line ->
            line.replace("# copy_examples=false", "copy_examples=false\ncopy_workspace=false")
        }
        into ""
    }
    // add desktop links
    from("src/main/resources/desktop-files") {
        into ""
    }
    // add scripts
    from("src/main/resources/scripts") {
        into ""
    }
}

buildRpm {
    dependsOn prepareRtiHost

    // define local installation path for links
    def applicationLinkPath        = "/usr/bin"
    def applicationLauncherPath    = "$targetDirectory/launcher"
    def applicationDesktopLinkPath = "/usr/share/applications"
    def applicationIconLinkPath    = "/usr/share/icons/hicolor/96x96/apps"

    // create link to license file
    link("$targetDirectory/rti_license.dat", "/opt/rti/connext-dds/license/rti_license.dat")

    // admin-console
    link("$applicationLinkPath/rtiadminconsole",                "$applicationLauncherPath")
    link("$applicationDesktopLinkPath/rtiadminconsole.desktop", "$targetDirectory/rtiadminconsole.desktop")
    link("$applicationIconLinkPath/rtiadminconsole.xpm",        "$targetDirectory/resource/app/app_support/admin_console/x64Linux/icon.xpm")
}

// --------------------------------------------------------------------------------------------------------------------
defaultTasks 'buildRpm'
